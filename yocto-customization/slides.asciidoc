= Custom Layers with Yocto
Zilogic Systems <training@zilogic.com>

== Build Your Own Layer and Image

=== Tiny Rootfs

 * As an experiment we can try to build the rootfs, which we had built
   earlier with Zepto, now using yocto. This would involve,

 ** building bash, coreutils & less.

 ** building the ext formatted rootfs

=== Creating Our Own Meta Layer

 * The better practise while working with yocto, is creating your own
   layer to hold your changes.

 * We can run the below command to create a layer called meta-zepto-fs
   in the build folder

[source,shell]
------
$ yocto-layer create zepto-fs
------

 * It prompts for various options which you can leave to default and
   this creates a directory meta-zepto-fs in the current directory.

 * We can find the layer.conf file inside the conf folder of
   meta-zepto-fs.

=== Need for Custom Image

 * As we know that, Yocto expects the list of packages to be built,
   should be provided through a variable IMAGE_INSTALL through a
   recipe or conf file.

 * core-image-minimal.bb was packing in lots of packages into rootfs

 * for our zepto-fs, we need only three packages bash, coreutils and
   less. It is better to create our own recipe to build rootfs image.

=== Creating Custom Image

 * We can check the recipe of core-image-minimal available in meta
   layer to create our own custom image

[source,shell]
------
$ cat meta/recipes-core/images/core-image-minimal.bb
------

 * It defines IMAGE_INSTALL & inherits core-image class

 * Yocto provides core-image class which can be inherited, to use
   already defined definitions to build rootfs image.

 * When we look on the layer.conf of our layer, we would find that the
   recipes are added to BBFILES from particular folder hierarchy.

------
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb"
------

=== Creating Custom Image

 * Sticking to Yocto's convention, we can create our image recipe
   named core-image-tiny.bb in meta-zepto-fs/recipes-core/images/
   folder

[source,shell]
------
$ mkdir -p meta-zepto-fs/recipes-core/images/
------

=== Knowing Supported Packages

 * The packages that are available in, currently added layers, can be
   obtained using

[source,shell]
----------
$ bitbake -s | grep -i coreutils
----------

 * From which we can get the package names for bash, coreutils and
   less.

=== Adding Packages to Recipe

 * In the image recipe file we can define the list of packages to be
   built into the rootfs image.

[source,shell]
------
$ vim meta-zepto-fs/recipes-core/images/core-image-tiny.bb


SUMMARY = "A tiny image just capable of allowing a device to boot."
IMAGE_INSTALL = " bash coreutils less "
inherit core-image
------

=== Handling Layers

 * We can list the layers added and their priority using

[source,shell]
------
$ bitbake-layers show-layers
------

 * We can add a layer to the current build as 

[source,shell]
------
$ bitbake-layers add-layer <path to the layer>
------


 * We can list the recipes based on their layers as 

[source,shell]
------
$ bitbake-layers show-recipes
------
 
=== Adding Our Layer to Yocto

 * The new layer created has distro configuration and the core-image
   recipe.

 * The meta-zepto-fs layer has to be added to the yocto build as

[source,shell]
------
$ bitbake-layers add-layer meta-zepto-fs
------

 * We can find that our layer is added into bblayers.conf

[source,shell]
-----
$ bitbake-layers show-layers
$ cat conf/bblayers.conf
-----

=== High Level Configuration of the Build

 * At-last the configurations for the current build has to be updated
   to the `build/conf/local.conf`

 * For our build we may need to provide two information 
   - target MACHINE for which this build performed and 
   - the DISTRO build policy to which adhered

 * We are going to build the rootfs for the qemu emulated arm target,
   which can be specified as `MACHINE = "qemuarm"`

=== Package Classes and Types

 * By default the distribution is built using opkg packages which is
   openembedded's packaging format, we can override that by declaring
   the `PACKAGE_CLASSES` as `package_deb`

 * If wanted to add any image specific features, like kinds of
   packages(doc, debug, etc) that has to be added to the image, we can
   specify it as shown below in local.conf or core-image-tiny.bb

-----
IMAGE_FEATURES = "doc-pkgs"
-----

=== Building Tiny-FS Image

 * Now we can start building the tiny-fs image by invoking

[source,shell]
--------
$ bitbake core-image-tiny
--------

 * The build should get completed in fewer minutes as it tries to
   reuse state-cache

 * After successful completion of the build the kernel image and
   rootfs images would be available in the
   tmp/deploy/images/qemuarm


=== Checking Our Tiny-FS Image

[source,shell]
--------------
$ ls tmp/deploy/images/qemuarm/
--------------

 * The list of packages in rootfs can be read from manifest file

[source,shell]
---------
$ vim tmp/deploy/images/qemuarm/core-image-tiny-qemuarm.rootfs.manifest
---------

=== Booting the Tiny-FS Image

 * Copy the zImage to shared folder /media/sf_shared and
   core-image-tiny-qemuarm.ext4 as disk.img in /media/sf_shared

 * Now we can boot our new images using qemu arm as shown

[source,shell]
------
$ ./runqemu.sh
------

=== Building Custom Packages

 * We can build packages which are compliant to autotools or cmake by
   inheritting the autotools or cmake classes in the recipe as we did
   for bash in zepto.

 * We can build packages with fewer source files by placing the source
   inside the layer itself.

 * We can build packages with custom build scripts by writing a recipe
   to invoke their build scripts in the build stage.


=== Recipes for In-layer Sources

 * We can build a package by placing the source of package along the
   recipe.

 * We can create a helloworld packages by creating a folder in our
   meta layer.

[source,shell]
-----
$ cd meta-zepto-fs/recipes-core
$ mkdir -p helloworld
$ mkdir -p heloworld/files 
-----

 * Fetcher can look for source files in the `files` folder residing in
   the same heirarchy of `bb` file.

=== Recipes for In-layer Sources Contd.

 * Create a 'C' file helloworld.c inside the `files` folder.

[source,shell]
-----
$ cd helloworld/files
$ vim helloworld.c
-----

[source,c]
-----
#include <stdio.h>

int main()
{
        printf("Hello World\n");
}

-----

=== Recipes for In-layer Sources Contd.

 * Create a helloworld.bb recipe file inside `helloworld` folder.

[source,shell]
-----
$ vim helloworld.bb
-----

[source,shell]
-----
PN="helloworld"
SRC_URI="file://helloworld.c"
-----

 * Specify the source file name using `file` protocol through `SRC_URI`,
   where the fetcher can fetch the file, from files folder to the
   `WORKDIR`.

 * The location of the source file has to be specified to the build
   stage, by explcitily pointing the `S` variable to the `WORKDIR`
   where the file was downloaded.

[source,shell]
-----
S = "${WORKDIR}"
-----

=== Recipes for In-layer Sources Contd.

 * We can specify our custom compilation commands and install commands
   through compile and install tasks.

[source,shell]
-----
do_compile() {
        ${CC} helloworld.c -o helloworld
}

do_install() {
        install -d ${D}${bindir}
        install -m 0755 helloworld ${D}${bindir}
}
-----

 * Install task needs to copy the built files into, `${D}` directory, in
   unix folder hierarchy locations as it should be placed in the
   target rootfs.

 * Yocto defines variables for `bin`, `etc` paths in variables
   `${bindir}`, `${systemdir}` etc.

 * Necessary folder can be created inside `${D}` using `install -d`
   command.

=== Recipes for In-layer Sources Contd.

 * By default yocto expects license for every package for it's QA
   proccess.

 * We can specify the license through `LICENSE` variable and md5
   checksum for the license file through `LIC_FILES_CHKSUM` variable.

 * Yocto has license files and its's checksum in its source itself,
   which can be referred, as in below example.

[source,shell]
-----
LICENSE = "MIT"
LIC_FILES_CHKSUM =
"file://${COMMON_LICENSE_DIR}/MIT;md5=0835ade698e0bcf8506ecda2f7b4f302"
-----

=== Recipes for In-layer Sources Contd.

[source,shell]
-----
PN="helloworld"

LICENSE = "MIT"
LIC_FILES_CHKSUM =
"file://${COMMON_LICENSE_DIR}/MIT;md5=0835ade698e0bcf8506ecda2f7b4f302"

SRC_URI="file://helloworld.c"

S = "${WORKDIR}"

do_compile() {
        ${CC} helloworld.c -o helloworld
}

do_install() {
        install -d ${D}${bindir}
        install -m 0755 helloworld ${D}${bindir}
}
-----

=== Recipe for Custom Built Packages

 * If we want to build a package which has its own build procedure,
   then it can also be handled by Yocto.

 * We can create a Makefile for our helloworld application and we can
   invoke it using Yocto.
 
[source,shell]
-----
$ cd helloworld/files
$ vim Makefile
-----

[source,shell]
-----
compile:
        ${CC} helloworld.c -o helloworld

install:
        install -d ${DESTDIR}/bin
        install -m 0755 helloworld ${DESTDIR}/bin
-----

=== Recipe for Custom Built Packages Contd.

[source,shell]
-----
PN="helloworld"

LICENSE = "MIT"
LIC_FILES_CHKSUM =
"file://${COMMON_LICENSE_DIR}/MIT;md5=0835ade698e0bcf8506ecda2f7b4f302"

SRC_URI="file://helloworld.c \
	 file://Makefile"

S = "${WORKDIR}"

do_configure[noexec] = "1"
do_clean[noexec] = "1"

do_compile() {
        oe_runmake compile
}

do_install() {
        oe_runmake install DESTDIR=${D}
}
-----

=== Bare Minimal local.conf

 * The yocto expects the version of the current configuration to be
   provided using CONF_VERSION

[source,shell]
------
S vim conf/local.conf

MACHINE = "qemuarm"
DISTRO = "poky"
CONF_VERSION = "1"
------

 * Below lines can be added to local.conf to modify that to build
   faster

-----
SSTATE_DIR = "/opt/sstate-cache"
BB_NUMBER_THREADS = "2"
-----

=== Customization on Distribution Level

 * If we want to different package management to be chosen, different
   init system to be chosen. These are defined to be the policy of the
   distribution.

 * For ex. Below command would configure yocto to build debian
   packages.

----
PACKAGE_CLASSES = "package_deb"
DISTRO_FEATURES_append = " sysvinit"
VIRTUAL-RUNTIME_init_manager = "sysvinit"
VIRTUAL-RUNTIME_dev_manager = "udev" 
----

=== Creating Our Own Distribution

 * By yocto convention, we may need to create a _distribution_ which has
   information about the policies to be adhered whiling building the
   individual components.

  * Distro configurations are more about how the packages are
    configured and built, rather than what packages are built.

  * Some of the distro configurations chooses how the system and
    packages has to be started in boot (sysv init or systemd)

  * Whether QT like UI components has to be compiled with X11 support
    or directfb support.

  * Certain distro features can depend or alter on HW related
    features.

=== Creating Our Own Distribution

  * We can create a new distribution called zebian in our meta-zepto-fs
    layer

  * Inside the meta-zepto-fs/conf create a directory distro and file
    zebian.conf inside it. Write below given distro descriptions to
    it.

[source,shell]
------
$ vim meta-zepto-fs/conf/distro/zebian.conf


DISTRO = "zebian"
DISTRO_VERSION = "1.0"
PACKAGE_CLASSES = "package_deb"
------

=== Adding Our Distro to local.conf

 * Since we don't have big policies thought out for our new
   distribution, we can just choose a packaging method for our distro.

 * Can edit the conf/local.conf to build our new distro as shown below

[source,shell]
------
$ vim conf/local.conf


MACHINE = "qemuarm"
DISTRO = "zebian"
CONF_VERSION = "1"
------

 * Build the core-image-tiny image and it would get built on
   `tmp-glibc`

 * Test the new distro on the target.
 
=== Handling Recipes in Multiple Layers

 * The recipes which are defined in both added layers are called
   overlayed layers. 

 * The overlayed recipe, in layer which has higher BB_FILE_PRIORITY
   value, would be considered for build.

 * So provding a higher prioriry value to local layers would make us
   to build our recipes over upstream recipes.

